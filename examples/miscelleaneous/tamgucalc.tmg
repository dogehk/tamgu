/@
 Project    : Spreadsheet
 filename   : tamgucalc.tmg
 Date       : 2020/06/15
 Purpose    : spreadsheet for terminals with Lisp expressions
 Programmer : Claude ROUX (claude.roux@naverlabs.com)
@/

//The Lisp interpreter
lisp lisp_interpreter;

string msgerr;

//Color definition to highlight parenthesis balancing
string colorred = _sys.colors(7,30,33, false);
string colornrm = _sys.colors(0,0,0,false);

//Initialisation: coords contains your terminal dimensions
ivector coords = _sys.screensize();
//columnsize is the size of columns. You can modify this value
int columnsize = 10;
//We initialise the number of lines and columns according to your terminal size and columnsize
ivector matrixsize=[coords[0]-6, coords[1]/columnsize];
//codeline is used to position messages
//6 lines down after the last line of the matrix
int codeline = matrixsize[0]+6;

//v_matrix is the visualisation matrix
vector v_matrix;
//formulas records the different Lisp expressions
mapss formulas;

//inputs is used to handle modifications on: filenames and size.
//If you want to enrich tamgucalc, you might add a new key
mapss inputs;
string inputkey;
inputs["Size"] = matrixsize[0]+":"+matrixsize[1];
inputs["File"] = _args[0];

if (_args[0] != "")
    inputs["Data(raw csv)"] = _args[0]+".csv";
else
    inputs["Data(raw csv)"] = "";

//mat is the actual matrix in which computing is done
fmatrix mat(matrixsize[0],matrixsize[1]);

//initialisation of v_matrix
function initialisation() {
    int i,j;
    string sc;
    for (i in <matrixsize[0]>) {
        v_matrix[i] = [];
        for (j in <matrixsize[1]>) {
            if (!i) {
                sc = j;
                v_matrix[i].push(sc);
            }
            elif (!j) {
                sc = i;
                v_matrix[i].push(sc);
            }
            else
                v_matrix[i].push("0");
        }
    }
}

//redimension of the x axis
function redimension_x(int x, int y) {
    int i,j;
    string sc;
    for (i in <matrixsize[0],x,1>) {
        v_matrix[i] = [];
        for (j in <y>) {
            if (!i) {
                sc = j;
                v_matrix[i].push(sc);
            }
            elif (!j) {
                sc = i;
                v_matrix[i].push(sc);
            }
            else
                v_matrix[i].push("0");
        }
    }
}

//redimension of the y axis
function redimension_y(int x, int y) {
    int i,j;
    string sc;
    for (j in <matrixsize[1],y,1>) {
        for (i in <x>) {
            if (!i) {
                sc = j;
                v_matrix[i].push(sc);
            }
            elif (!j) {
                sc = i;
                v_matrix[i].push(sc);
            }
            else
                v_matrix[i].push("0");
        }
    }
}

//Loading a file
function readtable(string f) {
    v_matrix.clear();
    try {
        svector sv;
        svector cut;
        int i,j;
        string s,k,e;
        sv.read(f);
        bool beg = true;
        for (s in sv) {
            cut=s.split("\t");
            if (beg == true) {
                //we have stored some parameters in the three first cells
                matrixsize[0] = cut[0];
                matrixsize[1] = cut[1];
                columnsize = cut[2];
                inputs["Size"] = matrixsize[0]+":"+matrixsize[1];
                codeline = matrixsize[0]+6;
                mat.dimension(matrixsize[0],matrixsize[1]);
                beg=false;
                //we need to reset them
                cut[0] = '0';
                cut[1] = '1';
                cut[2] = '2';
            }
            j = 0;
            v_matrix[i]=[];
            for (e in cut) {
                e=e.trim();
                if (e[0] == "(") {
                    k = i+"_"+j;
                    formulas[k] = e;
                    v_matrix[i].push("0");
                }
                else
                    v_matrix[i].push(e);
                j++;
            }
            i++;
        }
        evaluation();
    }
    catch {
        initialisation();
        displayall();
    }
}

//Writing the visual matrix content to a file 
function writetable(string f) {
    if (f=="") {
        displaymessage("Abort");
        return;
    }
    displaymessage("Writting: "+f);
    file sv(f,"w");
    int i,j;
    string k;
    v_matrix[0][0] = string(matrixsize[0]);
    v_matrix[0][1] = string(matrixsize[1]);
    v_matrix[0][2] = string(columnsize);
    for (i in <matrixsize[0]>) {
        for (j in <matrixsize[1]>) {
            if (j)
                sv.write("\t");
            k = i+"_"+j;
            if (formulas.test(k))
                sv.write(formulas[k]);
            else
                sv.write(v_matrix[i][j].trim());
        }
        sv.write("\n");
    }
    v_matrix[0][0] = "0";
    v_matrix[0][1] = "1";
    v_matrix[0][2] = "2";
}

//Writing only data to a CSV file. The result of formulas is stored
function writecsv(string f) {
    if (f=="") {
        displaymessage("Abort");
        return;
    }
    if ('.csv' not in f)
        f+=".csv";
    displaymessage("Writting (csv): "+f);
    file sv(f,"w");
    int i,j;
    string k;
    for (i in <1,matrixsize[0]>) {
        for (j in <1,matrixsize[1]>) {
            if (j)
                sv.write("\t");
            if (v_matrix[i][j][-1] == '!')
                sv.write(v_matrix[i][j][:-1].trim());
            else
                sv.write(v_matrix[i][j].trim());
        }
        sv.write("\n");
    }
}


//------------------- The Code ---------
//Formulas evaluation

function evaluation() {
    mat.clear();
    string ky;
    int i,j;
    for (i in <1,matrixsize[0]>) {
        for (j in <1,matrixsize[1]>) {
            ky = i+"_"+j;
            if (formulas.test(ky)) {
                mat[i:j] = 0;
            }
            else {
                mat[i:j] = float(v_matrix[i][j]);
            }
        }
    }
    if (formulas.size()==0) {
        displayall();
        return;
    }
    msgerr="";
    string msg;
    float val;
    int o;
    bool stop=false;
    
    //We record function beforehand
    for (ky in formulas) {
        i = ky[:"_"][:-1];
        j = ky["_":][1:];
        if ("defun " in formulas[ky]) {
            try {
                lisp_interpreter.eval(formulas[ky]);
                v_matrix[i][j] = formulas[ky][" ":"("][:-1].trim()+"!";
            }
            catch;
        }
    }

    //Not very efficient, we apply until nothing is modified again
    for (o in <formulas.size()>) {
        stop=true;
        for (ky in formulas) {
            //functions are skipped 
            if ("defun " in formulas[ky])
                continue;
            i = ky[:"_"][:-1];
            j = ky["_":][1:];
            try {
                val = lisp_interpreter.eval(formulas[ky]);
                if (val != mat[i:j])
                    stop=false;
                mat[i:j] = val;
                v_matrix[i][j] = string(val);
                v_matrix[i][j]+="!";
            }
            catch(msg) {
                msgerr=msg;
                v_matrix[i][j] = "#ERR";
            }
        }
        if (stop)
            break;
    }
    displayall();
    displayerr(msgerr);
}

//Display methods
//Display one element, we check its size
function dispelement(int i,int j) {
    string u = v_matrix[i][j];
    if (u.size() >= columnsize-1)
        u=u[:columnsize-2]+"_";
    print(u);
}

//Displaying all elements on screen
function displayall() {
    int i,j;
    _sys.cls();
    _sys.colors(0,0,0);
    for (i in <matrixsize[0]>) {
        for (j in <matrixsize[1]>) {
            _sys.row_column(i+1,columnsize*j);
            if (!i or !j)
                _sys.colors(7,96,40);
            dispelement(i,j);
            if (!i or !j)
                _sys.colors(0,0,0);
        }
    }
}

//Error message is displayed on line 4
function displayerr(string s) {
    _sys.row_column(codeline-4,0);
    _sys.eraseline(0);
    print(s);
}

//Messages are displayed on line 3
function displaymessage(string s) {
    _sys.row_column(codeline-3,0);
    _sys.eraseline(0);
    print(s);
}

//Highlight the current element 
function showelement(int i, int j) {
    string dsp;
    string ky = i+"_"+j;
    //We check if we are in a formula mode
    if (inputkey != "")
        dsp = inputkey+": "+inputs[inputkey];
    elif (formulas.test(ky)) {
        dsp = i+","+j+": "+formulas[ky];
    }
    else {//When we have a 0, we do not display it
        if (v_matrix[i][j].trim()=="")
            v_matrix[i][j]="0";
        if (v_matrix[i][j] == '0')
            dsp = i+","+j+": ";
        else {
            dsp = i+","+j+": "+v_matrix[i][j];
        }
    }

    //we use spaces to clean the cell first
    string space;
    space.padding(columnsize," ");
    _sys.row_column(i+1,columnsize*j);
    print(space);
    //Then we position our cursor again to display our value
    _sys.row_column(i+1,columnsize*j);
    //We use a specific color to show that it has been selected
    _sys.colors(7,31,49);
    dispelement(i,j);
    //Colors are reset, we then display on the last line the current value
    _sys.colors(0,0,0);
    _sys.row_column(codeline-1,0);
    _sys.eraseline(0);
    print(dsp);
}

//----------------------------------------------------------------------------------------------------
//MAIN LOOP

//the line 0 and the column 0 are used to display coordinates... 
int i = 1;
int j = 1;

//If a filename is provided, we try to read the file, otherwise we initialise our structure
if (inputs["File"]=="") {
    initialisation();
    displayall();
}
else
    readtable(inputs["File"]);

displaymessage("'(': Formula. Ctrl-s: Save. Ctrl-w: Save As. Ctrl-d: Save Raw Data. Ctrl-r Resize. Ctrl-q Quit.");

showelement(i,j);

string s=_sys.getchar();

bool selection = false;
string forme, ky;
int ci=-1,cj=-1;
int lasti, lastj;

while (s[0].ord() != 17) {

    //We use this position to display displaymessages
    displaymessage("'(': Formula. Ctrl-s: Save. Ctrl-w: Save As. Ctrl-d: Save Raw Data. Ctrl-r Resize. Ctrl-q Quit.");
    ky = i+"_"+j;

    //Using arrows
    if (s[0].ord() == 27) {
        _sys.row_column(i+1,columnsize*j);
        dispelement(i,j);
        if (s == _sys_keydel) {
            if (not selection) {
                v_matrix[i][j] = "0";
                if (formulas.test(ky))
                    formulas.pop(ky);
            }
            elif (i == ci and j == cj) {
                v_matrix[i][j] = "0";
                formulas.pop(ky);
                selection = false;
            }
            showelement(i,j);
            s = _sys.getchar();
            continue;
        }
        elif (s == _sys_keyright)
            j++;
        elif (s == _sys_keyleft)
            j--;
        elif (s == _sys_keyup)
            i--;
        elif (s == _sys_keydown)
            i++;

        i = max(i,1);
        i = min(i,matrixsize[0]-1);
        j = max(j,1);
        j = min(j,matrixsize[1]-1);
        showelement(i,j);
        s = _sys.getchar();
        continue;
    }

    //character deletion with the delete key
    if (s.ord() == 127) {
        if (inputkey!="") {
            inputs[inputkey]=inputs[inputkey][:-1];
        }
        else {
            //We are either modifying a formula or a cell
            if (formulas.test(ky)) {
                formulas[ky] = formulas[ky][:-1];
                if (formulas[ky] == "") {
                    //The formula has been deleted
                    formulas.pop(ky);
                    v_matrix[i][j] = "0";
                    selection=false;
                    ci =-1;
                    cj =-1;
                    _sys.row_column(i+1,columnsize*j);
                    //We display the "0" instead
                    dispelement(i,j);
                }
                else {
                    selection=true;
                    ci = i;
                    cj = j;
                }
            }
            else {
                if (!selection) {
                    if (v_matrix[i][j].size() > 1)
                        v_matrix[i][j] = v_matrix[i][j][:-1];
                    else
                        v_matrix[i][j] = "0";
                }
            }
        }
        showelement(i,j);
        s=_sys.getchar();
        continue;
    }

    //When introducing a formula, selection is true and the key is the formula position
    if (selection) {
        displaymessage("Use the arrows to select a cell and hit 'enter' to record it. Type '..' and select again to define a range.");
        ky = ci+"_"+cj;
    }

    //When hitting the Carriage Return key
    if (s == "\n") {
        if (inputkey=="Size") {
            ivector iv = inputs["Size"].split(":");
            inputkey="";
            if (iv[0] > matrixsize[0])
                redimension_x(iv[0],iv[1]);
            if (iv[1] > matrixsize[1])
                redimension_y(iv[0],iv[1]);
            matrixsize = iv;
            codeline = matrixsize[0]+6;
            displayall();
        }
        elif (inputkey == "Data(raw csv)") {
            writecsv(inputs["Data(raw csv)"]);
            inputkey="";
        }
        elif (inputkey=="File") {
            writetable(inputs["File"]);
            inputkey="";
        }
        else {
            //If we are in selection mode (formula) then we enable the cell selection with the arrows
            if (selection) {
                string subkey;
                //We test if a ".." has been introduced, in that case, we compute the interval between the structures
                if (lasti != -1 && formulas[ky][-2:] == '..') {
                    if (lasti > i)
                        [lasti,i] = [i,lasti];
                    if (lastj > j)
                        [lastj,j] = [j,lastj];

                    string frm;
                    int n;
                    bool beg=true;

                    formulas[ky] = formulas[ky][:-2];
                    //a single line
                    if (lasti == i) {
                        frm = "mat["+i+":]["+lastj+":"+(j+1)+"]";
                        formulas[ky][-"mat":] = frm;
                    }
                    elif (lastj == j) { //a single column
                        frm = "mat[:"+j+"]["+lasti+":"+(i+1)+"]";
                        formulas[ky][-"mat":] = frm;
                    }
                    else { //a square
                        n = lasti;
                        //&&& is the merge operator
                        frm = "(&&& ";
                        //we will add several lines together
                        while (n <= i) {
                            if (!beg)
                                frm += " ";
                            else
                                beg=false;
                            frm += "mat["+n+":]["+lastj+":"+(j+1)+"]";
                            n++;
                        }
                        frm += ")";
                        formulas[ky][-"mat":] = frm;
                    }
                }
                else
                    //One element at a time
                formulas[ky] += "mat["+i+":"+j+"] ";
                lasti=i;
                lastj=j;
                i = ci;
                j = cj;
            }
            else {
                displaymessage("eval");
                evaluation();
                _sys.row_column(i+1,columnsize*j);
                //You can add new values with the CR key
                //We showelement it to remove the selection color
                dispelement(i,j);
                //We move to the next cell below
                i++;
                i = max(i,1);
                i = min(i,matrixsize[0]-1);
            }
        }
        showelement(i,j);
        s=_sys.getchar();
        continue;
    }

    if (s[0].ord() < 27) {
        if (s[0].ord() == 4) {
            //ctrl-d
            inputkey="Data(raw csv)";
            _sys.row_column(codeline-1,0);
            print("Data(raw csv): ",inputs["Data"]);
        }
        elif (s[0].ord() == 19 and inputs["File"] != "") //ctrl-s
            writetable(inputs["File"]);
        elif (s[0].ord() == 19 or s[0].ord() == 23) { //ctrl-w/s
            _sys.row_column(codeline-1,0);
            print("File: ",inputs["File"]);
            inputkey="File";
        }
        elif (s[0].ord() == 18) { //ctrl-r
            _sys.row_column(codeline-1,0);
            print("Size: ",inputs["Size"]);
            inputkey="Size";
        }
        showelement(i,j);
        s=_sys.getchar();
        continue;
    }

    if (inputkey == "" and formulas.test(ky) and ci == i and cj == j) {
        formulas[ky]+=s;
        formulas[ky] = formulas[ky].replace(colorred,"");
        formulas[ky] = formulas[ky].replace(colornrm,"");
        //When the "(...)" balances, we exit the selection mode
        if (formulas[ky].count('(') > formulas[ky].count(")")) {
            if (s == ')') {
                int e = formulas[ky].size()-2;
                while (e>0 and formulas[ky][e:].count("(") != formulas[ky][e:].count(")")) e--;
                formulas[ky] = formulas[ky][:e]+colorred+formulas[ky][e]+colornrm+formulas[ky][e+1:];
            }
            selection = true;
            displaymessage("Use the arrows to select a cell and hit 'enter' to record it. Type '..' and select again to define a range.");
        }
        else {
            selection=false;
            displaymessage('Ready to compute!!!');
        }
        showelement(i,j);
        s=_sys.getchar();
        continue;
    }

    //We detect a '(', we enter the formula mode
    if (s=='(' and !selection) {
        ci = i;
        cj = j;
        lasti=-1;
        lastj=-1;
        formulas[ky] = s;
        selection = true;
        v_matrix[i][j] += "!";
        displaymessage("Use the arrows to select a cell and hit 'enter' to record it. Type '..' and select again to define a range.");
        showelement(i,j);
        s=_sys.getchar();
        continue;
    }

    //In case of pasting code
    if (s.size() > 2 and s[0] == '(') {
        s=s.trim();
        string u,k;
        //We try to detect from which place it was copied
        for (k in formulas) {
            if (formulas[k].trim() == s) {
                u=k;
                break;
            }
        }
        if (u != "") {
            u["_"]=":";
            string l= u[:":"];
            string c= u[":":];
            string rl = i+":";
            string rc = ":"+j;
            s=s.replace(l,rl);
            s=s.replace(c,rc);
            formulas[ky]=s;
            showelement(i,j);
            s=_sys.getchar();
            continue;
        }
    }

    if (inputkey != "")
        inputs[inputkey] += s;
    else {
        if (not selection) {
            if (v_matrix[i][j] == '0')
                v_matrix[i][j]=s;
            else
                v_matrix[i][j]+=s;
        }
    }
    showelement(i,j);
    s=_sys.getchar();
    continue;
}







